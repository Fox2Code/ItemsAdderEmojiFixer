plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
    id 'maven-publish'
}

group = 'com.fox2code'
version = '1.2.0'

repositories {
    mavenCentral()
    maven {
        name = 'purpur'
        url = 'https://repo.purpurmc.org/snapshots/'
    }
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'jitpack'
        url = 'https://jitpack.io/'
    }
    maven {
        name = 'placeholderapi'
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        name = "dmulloy2-repo"
        url = "https://repo.dmulloy2.net/repository/public/"
    }
    maven {
        name = 'Scarsz-Nexus'
        url = 'https://nexus.scarsz.me/content/groups/public/'
    }
    maven {
        name = 'essentials'
        url = 'https://repo.essentialsx.net/releases/'
    }
}

dependencies {
    implementation('org.json:json:20231013')
    implementation('com.vdurmont:emoji-java:5.1.1') {
        transitive = false
    }
    // compileOnly 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'org.purpurmc.purpur:purpur-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'com.comphenix.protocol:ProtocolLib:5.0.0'
    compileOnly 'it.unimi.dsi:fastutil:8.5.12'
    compileOnly 'com.discordsrv:discordsrv:+'
    compileOnly 'com.github.LoneDev6:api-itemsadder:3.6.1'
    compileOnly 'me.clip:placeholderapi:2.11.1'
    compileOnly 'net.essentialsx:EssentialsXDiscord:2.20.1'
    compileOnly 'net.essentialsx:EssentialsXChat:2.20.1'
    compileOnly('net.essentialsx:EssentialsX:2.20.1') {
        transitive = false
    }
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

jar.enabled = false

shadowJar {
    from "LICENSE"
    archiveClassifier.set('')
    relocate 'org.json', 'com.fox2code.itemsadderemojifixer.shadow.json'
    relocate 'com.vdurmont.emoji', 'com.fox2code.itemsadderemojifixer.shadow.emoji'
    minimize()
}

build.dependsOn(shadowJar)

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}